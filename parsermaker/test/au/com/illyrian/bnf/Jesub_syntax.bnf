{        
    goal ::= compilation_unit ;
    
    name:TerminalName ::= IDENTIFIER  { ast.Name($1) }
        ;
        
    qualified_name:AstExpression ::=
            name DOT qualified_name { ast.Dot($1, $3) }
        |   name    
        ;
        
    modifier_mult:AstModifiers ::=
            PUBLIC  modifier_mult            { ast.Modifier($1, $2) }
        |   PROTECTED  modifier_mult         { ast.Modifier($1, $2) }
        |   PRIVATE  modifier_mult           { ast.Modifier($1, $2) }
        |   ABSTRACT  modifier_mult          { ast.Modifier($1, $2) }
        |   FINAL  modifier_mult             { ast.Modifier($1, $2) }
        |   STRICTFP  modifier_mult          { ast.Modifier($1, $2) }
        |   /*EMPTY*/ 
        ;
    
    compilation_unit ::=
            package_opt import_mult class_declaration_plus { ast.Module($1, $2, $3) }
        ;
            
    package_opt:AstPackage ::= 
            PACKAGE qualified_name SEMI { ast.Package($2) }
        |   PACKAGE error("IncompletePackageName")
        |   /*EMPTY*/ 
        |   RECOVER(IMPORT | class_declaration)
        ;
    
    import_mult ::=
            IMPORT import_path_plus import_mult  { ast.Seq(ast.Import($2), $3) }
        |   IMPORT import_path_plus              { ast.Import($2) }
        |   /* EMPTY */
        |   RECOVER(IMPORT | class_declaration)
        ;
        
    import_path_plus:AstExpression ::= 
            name DOT import_path_plus            { ast.Dot($1, $3) }
        |   name SEMI                            { $1 }
        |   MULT SEMI                            { ast.Name("*") }
        |   error("IncompleteImportPath")
        ;
        
    class_declaration_plus ::= 
            class_declaration class_declaration_plus    { ast.Seq($1, $2) }
        |   class_declaration
        ;
        
    class_declaration ::=
            modifier_mult CLASS name extends_opt implements_opt class_body 
                                    { ast.DeclareClass($1, $3, $4, $5, $6) }
        |   modifier_mult INTERFACE name extends_opt class_body 
                                    { ast.DeclareInterface($1, $3, $4, $5) }
        |   error("ExpectedClassOrInterface") 
        ;
    
    extends_opt:AstExpression ::=
            EXTENDS qualified_name            {$2}
        |   EXTENDS error("ExpectedExtendedClassName")
        |   /*EMPTY*/
        |   RECOVER(IMPLEMENTS | BEGIN)
        ;
            
    
    implements_opt:AstExpression ::=
            IMPLEMENTS implements_plus  {$2}
        |   IMPLEMENTS error("ExpectedImplementedInterfaceName")
        |   /*EMPTY*/
        |   RECOVER(BEGIN)
        ;
    
    implements_plus:AstExpression ::=
            qualified_name COMMA implements_plus   { ast.Link($1, $3) }
        |   qualified_name
        ;
    
    class_body ::=
            BEGIN member_mult END               { $2 }
        ;
        
    member_mult ::=
            member member_mult                  { ast.Seq($1, $2) }
        |   /*EMPTY*/
        |   RECOVER(member)
        ;
        
    member ::=
            modifier_mult method_type name LPAR formal_mult RPAR method_body
                                        { ast.Method($1, $2, $3, $5, $7) }
        |   modifier_mult method_type name SEMI
                                        { ast.Declare($1, $2, $3) }
        ;
        
    method_type:AstExpression ::=
            type
        |   VOID                        { ast.Name("void") }
        ;
        
    type:AstExpression ::=
            class_type
        |   class_type LBRAC RBRAC      { ast.ArrayOf($1) }
        |   primitive_type
        |   primitive_type LBRAC RBRAC  { ast.ArrayOf($1) }
        ;
        
    primitive_type:AstExpression ::=
            BOOLEAN             { ast.Name("boolean") }
        |   BYTE                { ast.Name("byte") }
        |   CHAR                { ast.Name("char") }
        |   SHORT               { ast.Name("short") }
        |   INT                 { ast.Name("int") }
        |   LONG                { ast.Name("long") }
        |   FLOAT               { ast.Name("float") }
        |   DOUBLE              { ast.Name("double") }
        ;
    
    class_type:AstExpression ::= 
            qualified_name 
        ;
        
    formal_mult ::=
            formal_param formal_mult            { ast.Seq($1, $2) }
        |   /*EMPTY*/
        ;
    
    formal_param ::=
            modifier_mult type name             { ast.Declare($1, $2, $3) }
        ;
        
    method_body ::=
            compound_statement
        |   SEMI                                { null }
        ;
    
    compound_statement ::=
            BEGIN statement_mult END            { ast.Compound($2) }
        ;
    
    statement_mult ::=
            LOOKAHEAD(END) /*EMPTY*/
        |   statement statement_mult            { ast.Seq($1, $2) }
        ;
        
    statement ::=
            compound_statement
        |   if_else_statement
        |   try_statement
        |   break_statement
        |   continue_statement
        |   return_statement
//        |   declare_label_opt labeled_statement  { $2.setLabel($1) }
        |   labeled_statement
        |   SEMI                                 { null }
        |   declare_statement
        |   expression_statement
        |   RECOVER(statement)
        ;
    
    labeled_statement ::=
            while_statement
        |   for_statement
        ;
    /*
    declare_label_opt:String ::=
        //    LOOKAHEAD(IDENTIFIER COLON) name COLON             { ast.Label($1) } 
            COLON name                             { $2 } 
        |   /*EMPTY* /
        ;
    */
    if_else_statement ::=
            IF LPAR expr RPAR statement else_opt        { ast.If($3, $5, $6) }
        ;
        
    else_opt ::=
            ELSE statement                                    { $2 }
        |   /*EMPTY*/
        ;
    
    while_statement ::=
            WHILE LPAR expr RPAR statement  { ast.While($3, $5) }
        ;
    
    for_statement ::=
            FOR LPAR expression_opt SEMI expression_opt SEMI expression_opt RPAR statement
                                { ast.For($3, $5, $7, $9) }
        ;
    
    try_statement ::= 
            TRY compound_statement catch_plus finally_opt       { ast.Try($2, $3, $4) }
        ;
    
    catch_plus ::=
            catch_clause catch_plus                             { ast.Seq($1, $2) }
        |   catch_clause
        ;
    
    catch_clause ::=
            CATCH LPAR class_type name RPAR compound_statement { ast.Catch(ast.Declare($3, $4), $6) }
        ;
    
    finally_opt ::=
            FINALLY compound_statement                        { ast.Finally($2) }
        |   /*EMPTY*/
        ;
    
    break_statement ::=
            BREAK label_opt SEMI                         { ast.Break($2) }
        ;
    
    continue_statement ::=
            CONTINUE label_opt SEMI                      { ast.Continue($2) }
        ;
    
    label_opt:TerminalName ::=
            IDENTIFIER                          { ast.Name($1) }
        |   /*EMPTY*/
        ;
    
    return_statement ::=
            RETURN SEMI              { ast.Return(null) }
        |   RETURN expr SEMI         { ast.Return($2) }
        ;

    declare_statement ::=
            modifier_mult type name SEMI   { ast.Declare($1, $2, $3) }
        ;

    expression_statement ::=
            expr SEMI                { ast.Eval($1) }
        ;

    expression_opt:AstExpression ::=
            expr
        | /*EMPTY*/
        ;
        
    expr:AstExpression ::=
            expression()
        ;

    /* This is a line comment */
    // This is also a line comment
    /* This is a
     * multi-line comment
     */
}